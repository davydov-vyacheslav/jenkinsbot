plugins {
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "io.freefair.lombok" version "6.4.3"
    id "com.palantir.docker" version "0.13.0"
    id 'jacoco'
    id 'java'
}

group = 'com.javanix.bot'
version = '0.0.7-SNAPSHOT'
sourceCompatibility = '8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.github.pengrad:java-telegram-bot-api:5.7.0'

    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}


task copyReadMe(type: Copy, description: "Copy Project Info file to resources") {
    from project.projectDir
    into 'src/main/resources'
    include 'CHANGELOG'
}

processResources.dependsOn(copyReadMe)

/// Docker configuration
dockerPrepare.dependsOn(bootJar)
docker {
    name "davs87/jenkinsbot:$version"
    tags "latest"
    copySpec.from("./build/libs/$project.name-$version" + ".jar").into("app")
}

task release() {
    if (!version.contains("-SNAPSHOT")) {
        dependsOn(dockerPush)
        mustRunAfter('dockerPushLatest')
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'dockerPushLatest') {
        release.dependsOn(task)
    }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.required = false
    }
}